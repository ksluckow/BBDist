apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0'
jar {
    manifest {
        attributes 'Implementation-Title': 'CFG Distance Analysis',
                   'Implementation-Version': version
    }
}

def sootJarName = 'soot-2.5.0.jar'

//create a single Jar with all dependencies
task fatJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'CFG Distance Analysis',  
        	'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

task prep << {
    println 'Preparing build'
    def libDir = new File(projectDir, 'lib')
    if(!libDir.exists())
        libDir.mkdirs()

    def f = new File(libDir, sootJarName)
    if (!f.exists()) {
        def sootURL = 'https://ssebuild.cased.de/nightly/soot/lib/soot-trunk.jar'
        println 'Downloading ' + sootJarName
        new URL(sootURL).withInputStream{ i -> f.withOutputStream{ it << i }}
        println 'Done downloading soot to ' + f.getAbsolutePath()
    }
}

dependencies {
	compile files('lib/' + sootJarName)
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}
